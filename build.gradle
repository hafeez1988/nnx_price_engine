plugins {
	id 'org.springframework.boot' version '2.3.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'com.diffplug.gradle.spotless' version '3.28.1'
	id 'ru.vyarus.quality' version '4.3.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'

group = 'com.nnx'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	compile('io.springfox:springfox-swagger2:2.6.1')
  	compile('io.springfox:springfox-swagger-ui:2.6.1')
  	compile('org.springframework.boot:spring-boot-starter-web')
  	compile('mysql:mysql-connector-java:5.1.37')

	implementation('org.springframework.boot:spring-boot-starter')
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile("org.springframework.integration:spring-integration-test:5.1.0.RELEASE")
	testCompile("com.h2database:h2")

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

/**
 * Static code analyser configurations: This configures quality tools 
 * using Checkstyle, PMD, CPD, SpotBugs. Gradle configurations can be 
 * found in http://xvik.github.io/gradle-quality-plugin/4.2.2/guide/config.
 */
quality {
  /* Turn on/off the static code analysis feature */
  enabled = true

  /* Enable/disable required static code analysis tools */
  checkstyle = true
  pmd = true
  cpd = true
  spotbugs = true

  /* Enable/disable build failure on any violations. Build doesn't fail if this is disabled (false) */
  strict = false

  /* Enable/disable static code analysis console error logs */
  consoleReporting = false

  /* Enable/disable html reports. The xml reports will be generated by default */
  htmlReports = true

  /* Applies static analysis only for the specified project sources */
  sourceSets = [
    project.sourceSets.main
  ]
}

/**
 * Source code formatter: This configures the formatter for the source codes. 
 * Run this gradle command to initiate the execution of the formatter.
 *
 * `gradle spotlessApply`
 */
spotless {
  /* Skip the code formatting during gradle build */
  enforceCheck false

  /* Java source code formatter configurations */
  java {
    importOrder 'java', 'com.nnx'
    removeUnusedImports()
    eclipse().configFile 'formatter/NnxEclipseFormatter.xml'
  }
}
